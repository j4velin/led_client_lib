apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

def versionStr = "1.0.4"
def libName = 'LedClientLib'
def siteUrl = 'https://github.com/j4velin/led_client_lib'       // Homepage URL of the library
def gitUrl = 'https://github.com/j4velin/led_client_lib.git'    // Git repository URL
def group = "de.j4velin.LedClientLib"                           // Maven Group ID for the artifact

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName versionStr

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "com.squareup.okhttp3:okhttp:4.4.0"
    api "com.google.code.gson:gson:2.8.5"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact sourcesJar
                artifact javadocJar

                // You can then customize attributes of the publication as shown below.
                groupId = group
                artifactId = 'lib'
                version = versionStr

                pom {
                    name = libName
                    description = 'Android Library for applications to send events to the led_server'
                    url = siteUrl
                    packaging = 'aar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'j4velin'
                            name = 'Thomas Hoffmann'
                            email = 'thomas@j4velin.de'
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
} else {
    logger.warn('local.properties file not found')
}

// https://github.com/bintray/gradle-bintray-plugin
version = versionStr
bintray {
    dryRun = false
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['release']
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = libName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            vcsTag = versionStr
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
        }
    }
}

